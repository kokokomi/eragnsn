; 敵が狙うターゲットを選ぶ関数
; すべて#FUNCTION
; 戻り値はcharactorNumで、ターゲットを選べない場合は-1を返す

; ランダムで選ぶ
@enemy_target_strategy_random
  #FUNCTION
  RETURNF RAND:(party_get_member_count())

  ; 捕獲されていないキャラの中からランダム
@enemy_target_strategy_random_not_captured
  #FUNCTION
  #DIM lcount
  #DIM resultList, 4
  #DIM resultCount
  lcount = 0
  resultCount = 0
  FOR lcount, 0, party_get_member_count()
    IF CFLAG:(lcount):拘束 != -1 || CFLAG:(lcount):丸呑み != -1
      CONTINUE
    ENDIF
    resultList:(resultCount) = lcount
    resultCount++
  NEXT

  IF resultCount == 0
    RETURNF -1
  ENDIF

  RETURNF resultList:(RAND:(resultCount))

  ; 捕獲されているキャラの中で、自分で捕獲しているものを除く
@enemy_target_strategy_random_captured_exclude_self(enemyNum)
  #FUNCTION
  #DIM enemyNum
  #DIM lcount
  #DIM resultList, 4
  #DIM resultCount
  lcount = 0
  resultCount = 0
  FOR lcount, 0, party_get_member_count()
    ; 捕獲されている
    IF CFLAG:(lcount):拘束 != -1 && !CFLAG:(lcount):丸呑み != -1
      CONTINUE
    ENDIF
    ; 自分で捕獲している
    IF CFLAG:(lcount):拘束 == enemyNum || CFLAG:(lcount):丸呑み == enemyNum
      CONTINUE
    ENDIF
    resultList:(resultCount) = lcount
    resultCount++
  NEXT

  IF resultCount == 0
    RETURNF -1
  ENDIF

  RETURNF resultList:(RAND:(resultCount))

  ; 自分で捕獲しているものからランダム
@enemy_target_strategy_random_self_captured(enemyNum)
  #FUNCTION
  #DIM enemyNum
  #DIM lcount
  #DIM resultList, 4
  #DIM resultCount
  lcount = 0
  resultCount = 0
  FOR lcount, 0, party_get_member_count()
    ; 自分で捕獲している
    IF CFLAG:(lcount):拘束 == enemyNum || CFLAG:(lcount):丸呑み == enemyNum
      resultList:(resultCount) = lcount
      resultCount++
    ENDIF
  NEXT

  IF resultCount == 0
    RETURNF -1
  ENDIF

  RETURNF resultList:(RAND:(resultCount))