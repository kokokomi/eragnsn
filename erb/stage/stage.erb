; ステージの攻略
; erb/stage/stages/stage_{stageId}.erbを攻略する対象とする
; ステージの実装は/erb/stage/stages/doc.mdを参照
@stage(stageId)
  #DIM stageId
  #DIM lcount
  ステージID = stageId
  ステージ探索度 = 0
  ステージ現在フロア = 1
  FOR lcount, 0, party_get_member_count()
    CALL kojo_ステージ_秘境に入った(lcount, ステージID)
  NEXT

  WHILE true
    CALL inputLoop("show_stage", "stage_command")
    CALL party_reset_ep_if_ecstasy

    ; 全滅チェック
    IF party_is_defeat()
      CALL message_stage_defeat(ステージID, ステージ現在フロア)
      BREAK
    ENDIF 

    IF ステージ現在フロア >= ステージ定義:(ステージID):最大階層 && ステージ探索度 >= 100
      ; ステージクリア！
      LOCAL:0 = ステージ実績:(ステージID):クリア
      ステージ実績:(ステージID):クリア = true
      ステージ実績:(ステージID):最高クリア階層 = ステージ定義:(ステージID):最大階層
      CALL message_stage_clear(ステージID)
      FOR lcount, 0, party_get_member_count()
        CALL kojo_ステージ_クリア(lcount, ステージID, LOCAL:0 == false)
      NEXT

      CALLFORM stage_clear_{ステージID}(LOCAL:0 == false)
      BREAK
    ENDIF

    ; 次の階層へ
    IF ステージ探索度 >= 100
      ステージ探索度 = 0
      ステージ実績:(ステージID):最高クリア階層 = MAX(ステージ実績:(ステージID):最高クリア階層, ステージ現在フロア)
      ステージ現在フロア++
      FOR lcount, 0, party_get_member_count()
        CALL kojo_ステージ_次のフロア(lcount, ステージID)
      NEXT
    ENDIF
  WEND

  ステージID = 0
  ステージ探索度 = 0
  ステージ現在フロア = 0

@show_stage
  CALL show_floor_gui(ステージID)
  PRINTFORML [1] - 探索を進める
  PRINTFORML [2] - 周辺を探索

@stage_command(command)
  #DIM command
  SELECTCASE command
    CASE 1
      ; 探索を進める
      CALL stage_command_foward(ステージID)
      RETURN true
    CASE 2
      ; 周辺を探索
      CALL stage_command_explore(ステージID)
      RETURN true
  ENDSELECT

@stage_command_foward(stageId)
  #DIM stageId
  #DIM lcount

  CALL message_stage_select_foward
  FOR lcount, 0, party_get_member_count()
    CALL kojo_ステージ_先へ進む(lcount, stageId)
  NEXT

  CALLFORM stage_foward_{stageId}

@stage_command_explore(stageId)
  #DIM stageId
  #DIM lcount
  CALL message_stage_select_explore
  FOR lcount, 0, party_get_member_count()
    CALL kojo_ステージ_周囲を探索(lcount, stageId)
  NEXT

  CALLFORM stage_explore_{stageId}
  IF !RESULT
    PRINTFORML 何も見つからなかった
  ENDIF

@show_floor_gui(stageId)
  #DIM lcount
  #DIM memberCount
  #DIM stageId

  DRAWLINE
  PRINTFORML << %ステージ名:(stageId)%攻略中 >>
  PRINTFORML {ステージ現在フロア}F {ステージ探索度}\%
  memberCount = party_get_member_count()
  FOR lcount, 0, memberCount
    PRINTFORML %charactor_get_charactor_name(lcount)%
    CALL view_component_statusbar(lcount)
    PRINTL
  NEXT
  DRAWLINE