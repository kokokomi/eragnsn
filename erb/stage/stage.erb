; ステージの攻略
; erb/stage/stages/stage_{stageId}.erbを攻略する対象とする
; ステージの実装は/erb/stage/stages/doc.mdを参照
@stage(stageId)
#DIM stageId
#DIM command
FLAG:探索度 = 0
FLAG:現在のフロア = 1

$INPUT_LOOP
CALL show_floor_gui(stageId)
PRINTFORML [1] - 探索を進める
PRINTFORML [2] - 周辺を探索

INPUT
command = RESULT

SELECTCASE command
CASE 1
    ; 探索を進める
    CALL stage_command_foward(stageId)
CASE 2
    ; 周辺を探索
    CALL stage_command_explore(stageId)
ENDSELECT

; ターンが終わったとき
; 必要に応じてEPリセット
CALL party_reset_ep_if_ecstasy

; 全滅チェック
IF party_is_defeat()
    CALL message_stage_defeat(stageId, FLAG:現在のフロア)
    GOTO STAGE_FINISH
ENDIF 

IF FLAG:探索度 >= 100
    FLAG:探索度 = 0
    FLAG:現在のフロア++
    ; 次の階層へ
ENDIF

CALLFORM stage_get_max_floor_{stageId}
IF FLAG:現在のフロア >= RESULT && FLAG:探索度 >= 100
    ; ステージクリア！
    CALL message_stage_clear(stageId)
    GOTO STAGE_FINISH
ENDIF

GOTO INPUT_LOOP

$STAGE_FINISH
FLAG:探索度 = 0
FLAG:現在のフロア = 0

@stage_command_foward(stageId)
#DIM stageId
CALL message_stage_select_foward
CALLFORM stage_foward_{stageId}

@stage_command_explore(stageId)
#DIM stageId
CALL message_stage_select_explore
CALLFORM stage_explore_{stageId}
IF !RESULT
    PRINTFORML 何も見つからなかった
ENDIF

@show_floor_gui(stageId)
#DIM lcount
#DIM memberCount
#DIM stageId

DRAWLINE
CALLFORMF stage_{stageId}_stage_name
PRINTFORML << %RESULTS%攻略中 >>
PRINTFORML {FLAG:現在のフロア}F {FLAG:探索度}\%
memberCount = party_get_member_count()
FOR lcount, 0, memberCount
    PRINTFORML %charactor_get_charactor_name(lcount)%
    CALL view_component_statusbar(lcount)
    PRINTL
NEXT
DRAWLINE