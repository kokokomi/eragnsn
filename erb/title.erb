@SYSTEM_TITLE
  CALL print_debug("SYSTEM_TITLE")
  [IF_DEBUG]
  IF EXISTFUNCTION("run_testcases")
    CALL run_testcases
    QUIT
  ENDIF
  [ENDIF]

  ;@SYSTEM_TITLEが定義されていると、標準のタイトル画面の代わりにSYSTEM_TITLEが呼ばれる。

  ;このタイミングでグローバル変数を読んでおけば取りこぼしが無くなる。
  ;GLOBALはRESETDATAやLOADDATAによって初期化・上書きされない。
  ;必要に応じてコメントアウトを解除すること。
  ;LOADGLOBAL

  ;バージョン表記をLOCALS:0に作成。
  ;1001なら1.001、1100なら1.10と表示されるように。
  ;ここで使用した変数はRESETDATAかLOADDATAによってクリアされるのでローカル変数を使わなくてもよい。
  LOCAL:0 = GAMEBASE_VERSION / 1000
  LOCAL:1 = GAMEBASE_VERSION % 1000 / 10
  LOCAL:2 = GAMEBASE_VERSION % 10
  STRLENFORM {LOCAL:1}
  LOCALS:1 = %"0" * (2 - RESULT:0)%{LOCAL:1}
  LOCALS:0 = v{LOCAL:0}.%LOCALS:1%
  SIF LOCAL:2 != 0
    LOCALS:0 = %LOCALS:0%.{LOCAL:2}

  ;タイトル表示。
  DRAWLINE 

  ALIGNMENT CENTER
  PRINTFORML %GAMEBASE_TITLE%
  PRINTFORML %LOCALS:0%
  PRINTFORML %GAMEBASE_AUTHOR%
  PRINTFORML (%GAMEBASE_YEAR%)
  PRINTL 
  PRINTFORML %GAMEBASE_INFO%
  ALIGNMENT LEFT

  DRAWLINE 

  $TITLE_SELECT
  PRINTL [0] 最初からはじめる
  PRINTL [1] ロードしてはじめる

  $TITLE_INPUT
  INPUT

  IF RESULT == 0
    RESETDATA
    CALL initialize_global
    BEGIN FIRST
    RETURN
  ELSEIF RESULT == 1
    RESETDATA
    CALL initialize_global
    CALL LOADGAME_EX
    RESTART
  ELSE
    REUSELASTLINE 無効な値です
    GOTO TITLE_INPUT
  ENDIF