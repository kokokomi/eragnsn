;お店（という名の待機画面）処理

@EVENTSHOP
  CALL print_debug("EVENTSHOP")
  DRAWLINE

@SHOW_SHOP
  #DIM lcount
  #DIM lcount2

  CALL AUTO_SAVE

  FOR lcount, 0, party_get_member_count()
    FOR lcount2, 0, VARSIZE("BAD_STATUS")
      CALL charactor_reset_bad_status(lcount, BAD_STATUS:(lcount2), true)
    NEXT
    BASE:(lcount):0 = MAXBASE:(lcount):0
    BASE:(lcount):1 = MAXBASE:(lcount):1
    BASE:(lcount):2 = MAXBASE:(lcount):2
    BASE:(lcount):3 = MAXBASE:(lcount):3
    BASE:(lcount):4 = MAXBASE:(lcount):4
  NEXT
  CALL print_debug("SHOW_SHOP")
  CALL show_home_gui

  ; 0-99の値が入力されるとUSERSHOPが呼び出されないので100からスタートする
  PRINTFORMLC [100] - 探索へ向かう
  PRINTL
  PRINTFORMLC [101] - 編成
  PRINTFORMLC [102] - 招待
  PRINTL
  PRINTFORMLC [200] - セーブ
  PRINTFORMLC [300] - ロード

@USERSHOP
  #DIM command
  command = RESULT

  CALL print_debug("USERSHOP")

  SELECTCASE command
    CASE 100
      CALL select_stage
    CASE 101
      CALL organize
    CASE 102
      CALL charactor_shop
    CASE 200
      CALL SAVEGAME_EX
    CASE 300
      CALL LOADGAME_EX
  ENDSELECT

@show_home_gui
  #DIM memberCount
  #DIM lcount
  DRAWLINE
  PRINTFORMLC 調査開始から {DAY}日目
  PRINTFORMLC 資金: {MONEY:0}モラ
  PRINTL
  DRAWLINE
  PRINTFORML << 編成 >>
  memberCount = party_get_member_count()
  FOR lcount, 0, memberCount
    CALL show_charactor_status_for_home(party_get_member_nth(lcount), lcount)
  NEXT
  DRAWLINE

@show_charactor_status_for_home(charactorId, charactorNum)
  #DIM charactorId
  #DIM charactorNum
  SETCOLOR CFLAG:(charactorNum):キャラの色
  PRINTFORML %CALLNAME:(charactorNum)%

  RESETCOLOR
  LOCAL:0 = (EXP:(charactorNum):経験値) * 100 / required_charactor_exp(charactorNum)
  PRINTFORM {EXP:(charactorNum):キャラクターレベル}Lv {LOCAL:0}\%
  CALL view_component_colorbar(EXP:(charactorNum):0, required_charactor_exp(charactorNum), 10, COLOR_TEXT, TEXT_DISABLE)
  PRINTL

  CALL view_component_statusbar(charactorNum)
  PRINTL
