; 戦闘画面
; battleを呼ぶ前にbattle_setupをする必要がある
@battle
#DIM lcount
CALL print_debug("battle")
CALL battle_init

$TURNLOOP
CALL battle_player_turn
CALL battle_enemy_turn

FOR lcount, 0, party_get_member_count()
    ; 状態異常の残りターン数更新
    CFLAG:(lcount):恍惚 = MAX(0, CFLAG:(lcount):恍惚 - 1)
    CFLAG:(lcount):麻痺 = MAX(0, CFLAG:(lcount):麻痺 - 1)
    CFLAG:(lcount):凍結 = MAX(0, CFLAG:(lcount):凍結 - 1)
    CFLAG:(lcount):羞恥 = MAX(0, CFLAG:(lcount):羞恥 - 1)

    ; クールタイム更新
    CALL charactor_decrease_cooltime(lcount)
NEXT

; ターンエンド
CALL party_reset_ep_if_ecstasy
; PT全滅
IF party_is_defeat()
    CALL print_debug("@battle PT全滅")
    GOTO BATTLE_FINISH
ENDIF
; 敵全滅
IF battle_is_enemy_dead_all()
    CALL print_debug("@battle 勝利！")
    GOTO BATTLE_FINISH
ENDIF

;次のターンへ
GOTO TURNLOOP

$BATTLE_FINISH
CALL print_debug("battle finish")
CALL battle_cleanup
FOR lcount, 0, party_get_member_count()
    CFLAG:(lcount):拘束 = 0
    CFLAG:(lcount):丸呑み = 0
    CFLAG:(lcount):恍惚 = 0
    CFLAG:(lcount):麻痺 = 0
    CFLAG:(lcount):凍結 = 0
    CFLAG:(lcount):羞恥 = 0
NEXT

; 敵やプレイヤーのステータス表示
@show_battle_gui
#DIM lcount
#DIM enemy_count
#DIM memberCount

#DIM enemyLv
DRAWLINE

enemy_count = battle_enemy_count()
FOR lcount, 0, enemy_count
    PRINTFORM 【%battle_enemy_name(lcount)% {battle_enemy_level(lcount)}Lv】
    PRINTFORM {battle_enemy_hp(lcount)} / {battle_enemy_max_hp(lcount)} 
    CALL view_component_colorbar(battle_enemy_hp(lcount), battle_enemy_max_hp(lcount), 20, COLOR_HP, COLOR_HP_BG)
    PRINTL
NEXT

DRAWLINE

memberCount = party_get_member_count()
FOR lcount, 0, memberCount
    PRINTFORML 【%charactor_get_charactor_name(lcount)%】
    CALL view_component_statusbar(lcount)
    PRINTL
NEXT

; プレイヤーのターン
@battle_player_turn
#DIM charactorNum
#DIM lcount
#DIM memberCount

memberCount = party_get_member_count()

FOR lcount, 0, memberCount
    ; 放心チェック
    IF charactor_is_faint(lcount)
        CALL message_skip_player_turn_faint(lcount)
        WAIT
        CONTINUE
    ENDIF
    ; 体力が尽きている場合、ランダムな確率で行動不能
    IF charactor_is_weak(lcount) && RAND:5 < 1
        CALL message_skip_player_turn_weak(lcount)
        WAIT
        CONTINUE
    ENDIF
    IF CFLAG:(lcount):恍惚 > 0
        CALL message_skip_player_turn_ecstasy(lcount)
        WAIT
        CONTINUE
    ENDIF

    CALL battle_charactor_command(lcount)
NEXT

; 各キャラクターのコマンド
@battle_charactor_command(charactorNum)
#DIM charactorNum
#DIM enemy_count
#DIM lcount
#DIM enemyLv
#DIM command
#DIM targetEnemy

CALL show_battle_gui
DRAWLINE

PRINTFORML << %charactor_get_charactor_name(charactorNum)%のターン >>
PRINTFORML [1] 通常攻撃 
IF charactor_can_use_skill(charactorNum)
    PRINTFORML [2] %CSTR:(charactorNum):元素スキル%
ELSE
    PRINTFORML [x] %CSTR:(charactorNum):元素スキル%はまだ使えない
ENDIF

IF charactor_can_use_burst(charactorNum)
    PRINTFORML [3] %CSTR:(charactorNum):元素爆発%
ELSE
    PRINTFORML [x] %CSTR:(charactorNum):元素爆発%はまだ使えない
ENDIF

INPUT
command = RESULT

PRINTFORML 誰を狙う？
enemy_count = battle_enemy_count()
FOR lcount, 0, enemy_count
    PRINTFORM [{lcount}]
    PRINTFORM 【%battle_enemy_name(lcount)% {battle_enemy_level(lcount)}Lv】
    CALL view_component_colorbar(battle_enemy_hp(lcount), battle_enemy_max_hp(lcount), 20, COLOR_HP, COLOR_HP_BG)
    PRINTL
NEXT

INPUT
targetEnemy = RESULT

SELECTCASE command
    CASE 1
        CALLFORM normal_attack_{ABL:(charactorNum):武器 - 10}(charactorNum, targetEnemy)
    CASE 2
        IF charactor_can_use_skill(charactorNum)
            CALLFORM do_skill_{NO:(charactorNum)}(charactorNum, targetEnemy)
            ; スキルを発動したキャラに元素エネルギーを入れる
            CALL charactor_charge_energy(charactorNum, RAND:3 + 5)

            ; PTのキャラ全員に元素エネルギーを入れる
            FOR lcount, 0, party_get_member_count()
                CALL charactor_charge_energy(lcount, RAND:3 + 5)
            NEXT
            CFLAG:(charactorNum):元素スキルクールタイム = ABL:(charactorNum):スキルクールタイム
        ENDIF
    CASE 3
        IF charactor_can_use_burst(charactorNum)
            CALLFORM do_burst_{NO:(charactorNum)}(charactorNum, targetEnemy)
            CFLAG:(charactorNum):元素爆発クールタイム = ABL:(charactorNum):元素爆発クールタイム
            CFLAG:(charactorNum):元素エネルギー量 = 0
        ENDIF
ENDSELECT

; 敵のターン
@battle_enemy_turn
#DIM enemy_count
#DIM enemyId
#DIM lcount
#DIM enemyLv
enemy_count = battle_enemy_count()
FOR lcount, 0, enemy_count
    ; 敵が死んでいる場合行動スキップ
    IF battle_is_enemy_dead(lcount)
        CONTINUE
    ENDIF

    PRINTFORML << %battle_enemy_name(lcount)% {battle_enemy_level(lcount)}Lv のターン >>
    WAIT
    CALL enemy_select_command(battle_enemy_id(lcount), lcount)
    WAIT
NEXT

@battle_init
#DIM lcount
FOR lcount, 0, party_get_member_count()
    CALL charactor_reset_for_battle_start(lcount)
NEXT
