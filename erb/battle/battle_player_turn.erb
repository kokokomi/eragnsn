; プレイヤーのターン
@battle_player_turn
#DIM lcount

FOR lcount, 0, party_get_member_count()
    CALL kojo_戦闘_ターン開始(lcount)
NEXT

FOR lcount, 0, party_get_member_count()
    ; 放心チェック
    IF charactor_is_faint(lcount)
        SETCOLOR COLOR_CP
        PRINTFORML ━━━━　<< %charactor_get_charactor_name(lcount)%のターン >>　━━━━━━━━━━━━
        RESETCOLOR
        CALL message_skip_player_turn_faint(lcount)
        PRINTL
        WAIT
        CONTINUE
    ENDIF
    ; 体力が尽きている場合、ランダムな確率で行動不能
    IF charactor_is_weak(lcount) && RAND:5 < 1
        SETCOLOR COLOR_CP
        PRINTFORML ━━━━　<< %charactor_get_charactor_name(lcount)%のターン >>　━━━━━━━━━━━━
        RESETCOLOR
        CALL message_skip_player_turn_weak(lcount)
        PRINTL
        WAIT
        CONTINUE
    ENDIF
    IF charactor_has_bad_status(lcount, "恍惚")
        SETCOLOR COLOR_CP
        PRINTFORML ━━━━　<< %charactor_get_charactor_name(lcount)%のターン >>　━━━━━━━━━━━━
        RESETCOLOR
        CALL message_skip_player_turn_ecstasy(lcount)
        PRINTL
        WAIT
        CONTINUE
    ENDIF

    IF battle_is_enemy_dead_all()
        CONTINUE
    ENDIF

    CALL battle_charactor_command(lcount)
NEXT

; 各キャラクターのコマンド
@battle_charactor_command(charactorNum)
#DIM charactorNum
#DIM enemy_count
#DIM lcount
#DIM enemyLv
#DIM command
#DIM targetEnemy

CALL show_battle_gui
DRAWLINE
PRINTFORML << %charactor_get_charactor_name(charactorNum)%のターン >>
CALL kojo_戦闘_自分のターン開始(charactorNum)
IF !charactor_has_bad_status(charactorNum, "拘束") && !charactor_has_bad_status(charactorNum, "丸呑み")
    PRINTFORML [1] 通常攻撃 
    IF charactor_can_use_skill(charactorNum)
        PRINTFORML [2] %CSTR:(charactorNum):元素スキル%
    ELSE
        PRINTFORML [x] %CSTR:(charactorNum):元素スキル%はまだ使えない
    ENDIF

    IF charactor_can_use_burst(charactorNum)
        PRINTFORML [3] %CSTR:(charactorNum):元素爆発%
    ELSE
        PRINTFORML [x] %CSTR:(charactorNum):元素爆発%はまだ使えない
    ENDIF
ELSE
    PRINTFORML [4] 拘束を振りほどく
    PRINTFORML [5] されるがままに
ENDIF

INPUT
command = RESULT

PRINTFORML 誰を狙う？
enemy_count = battle_enemy_count()
FOR lcount, 0, enemy_count
    PRINTFORM [{lcount}]
    PRINTFORM 【%battle_enemy_name(lcount)% {battle_enemy_level(lcount)}Lv】
    CALL view_component_colorbar(battle_enemy_hp(lcount), battle_enemy_max_hp(lcount), 20, COLOR_HP, COLOR_HP_BG)
    PRINTL
NEXT

INPUT
targetEnemy = RESULT

SETCOLOR COLOR_CP
PRINTFORML ━━━━　<< %charactor_get_charactor_name(charactorNum)%のターン >>　━━━━━━━━━━━━
RESETCOLOR
SELECTCASE command
    CASE 1
        CALL kojo_戦闘_通常攻撃使用(charactorNum)
        CALLFORM normal_attack_{ABL:(charactorNum):武器 - 10}(charactorNum, targetEnemy)
    CASE 2
        IF charactor_can_use_skill(charactorNum)
            CALL kojo_戦闘_元素スキル使用(charactorNum)
            CALLFORM do_skill_{NO:(charactorNum)}(charactorNum, targetEnemy)
            ; スキルを発動したキャラに元素エネルギーを入れる
            CALL charactor_charge_energy(charactorNum, RAND:3 + 5)

            ; PTのキャラ全員に元素エネルギーを入れる
            FOR lcount, 0, party_get_member_count()
                CALL charactor_charge_energy(lcount, RAND:3 + 5)
            NEXT
            CFLAG:(charactorNum):元素スキルクールタイム = ABL:(charactorNum):スキルクールタイム
        ENDIF
    CASE 3
        IF charactor_can_use_burst(charactorNum)
            CALL kojo_戦闘_元素爆発使用(charactorNum)
            CALLFORM do_burst_{NO:(charactorNum)}(charactorNum, targetEnemy)
            CFLAG:(charactorNum):元素爆発クールタイム = ABL:(charactorNum):元素爆発クールタイム
            CFLAG:(charactorNum):元素エネルギー量 = 0
        ENDIF
    CASE 4
        IF RAND:5 == 0
            PRINTFORML 拘束を振りほどくことに成功した！
            CFLAG:(charactorNum):拘束 = -1
            CFLAG:(charactorNum):丸呑み = -1
        ELSE
            PRINTFORML 抵抗を封じられてしまった！
        ENDIF
    CASE 5
        PRINTFORML 抵抗をやめ、されるがままになっている
ENDSELECT

WAIT
PRINTL