; 戦闘開始前の設定を初期化する
@battle_setup
DT_CREATE "enemies"
DT_COLUMN_ADD "enemies", "enemy_id", "int16"
DT_COLUMN_ADD "enemies", "enemy_name"
DT_COLUMN_ADD "enemies", "enemy_level", "int16"
DT_COLUMN_ADD "enemies", "enemy_max_hp", "int16"
DT_COLUMN_ADD "enemies", "enemy_hp", "int16"
DT_COLUMN_ADD "enemies", "enemy_aura", "int16"
DT_COLUMN_ADD "enemies", "enemy_atk", "int16"

; 戦闘に敵を追加する
@battle_setup_add_enemy(enemyId, enemyLevel)
#DIM enemyId
#DIM enemyLevel
#DIM maxHp
#DIM enemyAtk

maxHp = 敵ステータス:(enemyId):体力 + (敵ステータス:(enemyId):体力加算値 * enemyLevel)
enemyAtk = 敵ステータス:(enemyId):攻撃力 + (敵ステータス:(enemyId):攻撃力加算値 * enemyLevel)

DT_ROW_ADD "enemies", "enemy_id", enemyId, "enemy_name", 敵の名前:(enemyId), "enemy_level", enemyLevel, "enemy_max_hp", maxHp, "enemy_hp", maxHp, "enemy_aura", 0, "enemy_atk", enemyAtk

@battle_enemy_name(enemyNum)
#FUNCTIONS
#DIM enemyNum
RETURNF DT_CELL_GETS("enemies", enemyNum, "enemy_name")

@battle_enemy_id(enemyNum)
#FUNCTION
#DIM enemyNum
RETURNF DT_CELL_GET("enemies", enemyNum, "enemy_id")

@battle_enemy_level(enemyNum)
#FUNCTION
#DIM enemyNum
RETURNF DT_CELL_GET("enemies", enemyNum, "enemy_level")

@battle_enemy_max_hp(enemyNum)
#FUNCTION
#DIM enemyNum
RETURNF DT_CELL_GET("enemies", enemyNum, "enemy_max_hp")

@battle_enemy_hp(enemyNum)
#FUNCTION
#DIM enemyNum
RETURNF DT_CELL_GET("enemies", enemyNum, "enemy_hp")

@battle_enemy_atk(enemyNum)
#FUNCTION
#DIM enemyNum
RETURNF DT_CELL_GET("enemies", enemyNum, "enemy_atk")

; 敵の数
@battle_enemy_count
#FUNCTION
DT_ROW_LENGTH "enemies"
RETURNF RESULT

; 終了時の後片付け
@battle_cleanup
DT_RELEASE "enemies"

; 戦闘の設定が正しいかのバリデーション
@validate_battle_configuration
#FUNCTION
DT_EXIST "enemies"
IF !RESULT
    RETURNF false
ENDIF

DT_ROW_LENGTH "enemies"
IF RESULT < 1
    RETURNF false
ENDIF
RETURNF true